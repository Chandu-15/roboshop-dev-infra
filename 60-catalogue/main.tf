resource "aws_instance" "catalogue"{
    ami=local.ami_id
    instance_type=var.instance_type
    vpc_security_group_ids=[local.catalogue_sg_id]
    subnet_id=local.private_subnet_id
        tags = merge (
        local.common_tags,
        {
            Name = "${var.project_name}-${var.environment}-catalogue"
        }
    )       
}

resource "terraform_data" "catalogue"{
    triggers_replace=[
        aws_instance.catalogue.id
    ]

    connection{
        type="ssh"
        user="ec2-user"
        password="DevOps321"
        host=aws_instance.catalogue.private_ip
    }
    provisioner "file"{
        source="bootstrap.sh"
        destination="/tmp/bootstrap.sh"
    }
    provisioner "remote-exec"{
        inline=[
            "chmod +x /tmp/bootstrap.sh",
            "sudo sh /tmp/bootstrap.sh catalogue dev"
        ]

    }
}
resource "aws_ec2_instance_state" "catalogue"{
    instance_id=aws_instance.catalogue.id
    depends_on=[terraform_data.catalogue]
    state="stopped"

}

resource "aws_ami_from_instance" "catalogue"{
    name="${local.common_name}-catalogue-ami"
    source_instance_id=aws_instance.catalogue.id
    depends_on=[aws_ec2_instance_state.catalogue]
    tags = merge (
        local.common_tags,
        {
            Name = "${var.project_name}-${var.environment}-catalogue"
        }
    )     
    
}

resource "aws_lb_target_group" "catalogue"{
    name="${local.common_name}-catalogue"
    vpc_id=local.vpc_id
    port="8080"
    protocol="HTTP"
    deregistration_delay=60
    health_check{
        healthy_threshold=2
        interval=10
        matcher="200-299"
        path="/health"
        timeout=2
        unhealthy_threshold=2
    }

}

resource "aws_launch_template" "catalogue"{
    name="${local.common_name}-catalogue"
    image_id=aws_ami_from_instance.catalogue.id
    vpc_security_group_ids=[local.catalogue_sg_id]
    instance_type="t3.micro"
    instance_initiated_shutdown_behavior="terminate"
    tag_specifications{
        resource_type="instance"
        tags = merge (
            local.common_tags,
            {
                Name = "${var.project_name}-${var.environment}-catalogue"
            }
    )     

    }
    tag_specifications{
        resource_type="volume"
        tags = merge (
            local.common_tags,
            {
                Name = "${var.project_name}-${var.environment}-catalogue"
            }
    )     

    }
    tags = merge (
            local.common_tags,
            {
                Name = "${var.project_name}-${var.environment}-catalogue"
            }
    )
    
}

resource "aws_autoscaling_group" "catalogue"{
    name="${local.common_name}-catalogue"
    min_size=1
    max_size=10
    desired_capacity=1
    health_check_grace_period=100
    health_check_type="ELB"
    force_delete=false
    launch_template{
        id=aws_launch_template.catalogue.id
        version=aws_launch_template.catalogue.latest_version
    }
    target_group_arns=[aws_lb_target_group.catalogue.arn]
    vpc_zone_identifier=local.private_subnet_ids
    instance_refresh{
        strategy="Rolling"
        preferences{
            min_healthy_percentage=50
        }
        triggers=["launch_template"]
    }

    dynamic "tag"{
        for_each= merge (
            local.common_tags,
            {
                Name = "${var.project_name}-${var.environment}-catalogue"
            }
        )
        content{
            key=tag.key
            value=tag.value
            propagate_at_launch=true
        }
    }
    timeouts{
        delete="15m"
    }
}

resource "aws_autoscaling_policy" "catalogue"{
    name="${local.common_name}-catalogue"
    autoscaling_group_name=aws_autoscaling_group.catalogue.name
    policy_type="TargetTrackingScaling"
    target_tracking_configuration{
        predefined_metric_specification{
            predefined_metric_type="ASGAvgCPUUtilization"
        }
        target_value=75.0
    }
}

resource "aws_lb_listener_rule" "catalogue"{
    listener_arn=local.backend_alb_listener
    priority=10
    action{
        type="forward"
        target_group_arn=aws_lb_target_group.catalogue.arn
    }
    condition{
        host_header{
            values=["catalogue.backend-alb-${var.environment}.${var.domain_name}"]
        }
    }

}

resource "terraform_data" "catalogue_local" {
  triggers_replace = [
    aws_instance.catalogue.id
  ]
  
  depends_on = [aws_autoscaling_policy.catalogue]
  provisioner "local-exec" {
    command = "aws ec2 terminate-instances --instance-ids ${aws_instance.catalogue.id}"
  }
}